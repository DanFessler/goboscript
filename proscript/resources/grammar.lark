start : declr*

declr :            "costumes"      _stringlist ";"   -> declr_costumes
      |            "sounds"        _stringlist ";"   -> declr_sounds
      | [/nowarp/] "def"      NAME _namelist   stack -> declr_proc
      |            NAME            _exprlist   stack -> declr_hat

stack : "{" block* "}"

block : NAME _exprlist ";"

expr  : "(" expr ")"
      | NUMBER | BOOL | STRING
      | VAR                    -> var
      | ARG                    -> arg
      | NAME "(" _exprlist ")" -> reporter

_namelist   : [NAME ("," NAME)*]
_stringlist : [STRING ("," STRING)*]
_exprlist   : [expr ("," expr)*]

ARG        : /@[A-Za-z_0-9]*/
VAR        : /[a-zA-Z_.$][A-Za-z_.$0-9]*/
NAME       : /[a-zA-Z_][A-Za-z_0-9]*/
STRING     : /"([^"\\]|\\.)*"/
NUMBER     : /[0-9.]+/
BOOL       : "true" | "false"
ML_COMMENT : /\/\*.*?\*\//s

%ignore ML_COMMENT
%ignore " "
%ignore "\n"
%ignore "\t"
