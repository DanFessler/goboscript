start: costumes top*

costumes: "costumes" STRING ("," STRING)* _END

top : NAME stack                                                 -> hat
    | "def" NAME [NAME ("," NAME)*] stack                        -> funcdef

stack: "{" statement* "}"

statement : variable "=" expr ";"                                -> varset
          | variable "=" "[" "]" ";"                             -> lstset
          | variable ":" NAME [expr ("," expr)*] ";"             -> ofstatement
          | "if" expr stack                                      -> iff
          | "if" expr stack "else" stack                         -> ifelse
          | "if" expr stack ("elseif" expr stack)+               -> ifelseif
          | "if" expr stack ("elseif" expr stack)+ "else" stack  -> ifelseifelse
          | NAME [expr ("," expr)*] ";"

?expr : "(" expr ")"                                             -> exprgroup
      | "!" expr                                                 -> nott
      | expr "&" expr                                            -> andd
      | expr "|" expr                                            -> orr
      | expr "=" expr                                            -> eq
      | expr "<" expr                                            -> lt
      | expr ">" expr                                            -> gt
      | expr "++" expr                                           -> join
      | expr "+" expr                                            -> add
      | expr "-" expr                                            -> sub
      | expr "*" expr                                            -> mul
      | expr "/" expr                                            -> div
      | expr "%" expr                                            -> mod
      | "-" expr                                                 -> minus
      | NAME "(" [expr ("," expr)*] ")"                          -> reporter
      | variable ":" NAME "(" [expr ("," expr)*] ")"             -> ofreporter
      | variable
      | NUMBER
      | BOOL
      | STRING

variable: VARIABLE

_END:       ";"
VARIABLE:   /([a-zA-Z_][a-zA-Z0-9_]*)?[.$@]?[a-zA-Z_][a-zA-Z0-9_]*/
NAME:       /[a-zA-Z_][a-zA-Z0-9_]*/
STRING:     /"([^"\\]|\\.)*"/
NUMBER:     /[0-9]+/ | /[0-9]*.[0-9]+/
BOOL:       "true" | "false"
COMMENT:    /\/\*.*?\*\//s

%ignore COMMENT
%ignore " "
%ignore "\n"
%ignore "\t"
