start: toplevel*

toplevel: "costumes" STRING ("," STRING)* ";"            -> costumes
    | "sounds" STRING ("," STRING)* ";"                  -> sounds
    | "use" STRING ";"                                   -> use
    | [/nowarp/] "def" NAME [NAME ("," NAME)*] stack     -> deff
    | NAME _exprlist stack                               -> hat

stack: "{" statement* "}"

statement: NAME _exprlist ";"
    | NAME "=" expr ";"                       -> varset
    | NAME "+=" expr ";"                      -> varchange
    | NAME "-=" expr ";"                      -> varsub
    | NAME "*=" expr ";"                      -> varmul
    | NAME "/=" expr ";"                      -> vardiv
    | NAME "%=" expr ";"                      -> varmod
    | NAME "++=" expr ";"                     -> varjoin
    | "$"NAME "+=" expr ";"                   -> gvarchange
    | "$"NAME "-=" expr ";"                   -> gvarsub
    | "$"NAME "*=" expr ";"                   -> gvarmul
    | "$"NAME "/=" expr ";"                   -> gvardiv
    | "$"NAME "%=" expr ";"                   -> gvarmod
    | "$"NAME "++=" expr ";"                  -> gvarjoin
    | NAME "=" "[" "]" ";"                    -> lstset
    | NAME "[" expr "]" "=" expr ";"          -> lstchange
    | "$"NAME "[" expr "]" "=" expr ";"       -> glstchange
    | "$"NAME "=" expr ";"                    -> gvarset
    | "$"NAME "=" "[" "]" ";"                 -> glstset
    | "if" expr stack                         -> iff
    | "if" expr stack "else" stack            -> ifelse
    | "if" expr stack ("elseif" expr stack)+  -> ifelseif
    | "if" expr stack ("elseif" expr stack)+ "else" stack  -> ifelseifelse
    | "repeat" expr stack                     -> repeat
    | "until" expr stack                      -> until
    | "forever" stack                         -> forever
    | NAME "." NAME _exprlist ";"             -> ofstatement
    | "$"NAME "." NAME _exprlist ";"          -> gofstatement

?expr: "(" expr ")"                           -> parenexpr
    | NUMBER
    | BOOL
    | STRING
    | NAME "(" _exprlist ")"                  -> reporter
    | NAME "." NAME "(" _exprlist ")"         -> ofreporter
    | "$"NAME "." NAME "(" _exprlist ")"      -> gofreporter
    | NAME "[" expr "]"                       -> lstitem
    | "$"NAME "[" expr "]"                    -> glstitem
    | "!" expr                                -> nott
    | expr "&" expr                           -> andd
    | expr "|" expr                           -> orr
    | expr "=" expr                           -> eq
    | expr "<" expr                           -> lt
    | expr ">" expr                           -> gt
    | expr "++" expr                          -> join
    | expr "+" expr                           -> add
    | expr "-" expr                           -> sub
    | expr "*" expr                           -> mul
    | expr "/" expr                           -> div
    | expr "%" expr                           -> mod
    | "-" expr                                -> minus
    | "@"NAME                                 -> argument
    | "$"NAME                                 -> gvar
    | NAME                                    -> var

_exprlist: [expr ("," expr)*]

NAME:    /[a-zA-Z_][a-zA-Z0-9_]*/
STRING:  /"([^"\\]|\\.)*"/
NUMBER:  /[0-9.]+/
BOOL:    "true" | "false"
ML_COMMENT: /\/\*.*?\*\//s

%ignore ML_COMMENT
%ignore " "
%ignore "\n"
%ignore "\t"
