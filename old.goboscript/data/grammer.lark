start: toplevel*

toplevel: [/nowarp/] "def " NAME [NAME ("," NAME)*] stack         -> procdef
    | "use" STRING ";"                                            -> use
    | "costumes" STRING ("," STRING)* ";"                         -> costumes
    | "sounds" STRING ("," STRING)* ";"                           -> sounds
    | NAME _exprlist stack                                        -> hat

stack: "{" statement* "}"

statement: "forever" stack                                -> forever
    | "repeat" expr stack                                 -> repeat
    | "until" expr stack                                  -> until
    | "if" expr stack                                     -> iff
    | "if" expr stack "else" stack                        -> ifelse
    | "if" expr stack ("elseif" expr stack)+              -> ifelseif
    | "if" expr stack ("elseif" expr stack)+ "else" stack -> ifelseifelse
    | NAME "=" expr ";"                                   -> varset
    | NAME "+=" expr ";"                                  -> varchange
    | NAME "=" "[" _exprlist "]" ";"                      -> listset
    | NAME "[" expr "]" "=" expr ";"                      -> listchange
    | NAME "." NAME _exprlist ";"                         -> ofstatement
    | NAME _exprlist ";"

?expr: "(" expr ")"
    | "!" expr                        -> nott
    | expr "&" expr                   -> andd
    | expr "|" expr                   -> orr
    | expr "=" expr                   -> eq
    | expr "<" expr                   -> lt
    | expr ">" expr                   -> gt
    | expr "+" expr                   -> add
    | expr "++" expr                  -> join
    | expr "-" expr                   -> sub
    | "-" expr                        -> minus
    | expr "*" expr                   -> mul
    | expr "/" expr                   -> div
    | expr "%" expr                   -> mod
    | NAME "(" _exprlist ")"          -> reporter
    | NAME "." NAME "(" _exprlist ")" -> ofreporter
    | NAME "[" _exprlist "]"          -> listget
    | "@" NAME                        -> argument
    | NUMBER
    | BOOL
    | NAME
    | STRING

NAME:   /[a-zA-Z0-9_]+/
NUMBER: /[0-9.]+/
STRING: /"([^"\\]|\\.)*"/
BOOL:   "true" | "false"

_exprlist: [expr ("," expr)*]

%ignore " "
%ignore "\n"
%ignore "/*" /(.|\n)+/ "*/"
    | "//" /.+/ "\n"
