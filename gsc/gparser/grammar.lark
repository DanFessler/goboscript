start : declr*

declr :            "costumes"      _stringlist ";"   -> declr_costumes
      |            "sounds"        _stringlist ";"   -> declr_sounds
      | [/nowarp/] "def"      NAME _namelist   stack -> declr_proc
      |            NAME            _exprlist   stack -> declr_hat

stack : "{" block* "}"

block : NAME _exprlist ";"
      | VAR                       "=" expr     ";" -> block_setvar
      | VAR              OPERATOR "=" expr     ";" -> block_changevar
      | VAR                       "=" "["  "]" ";" -> block_setlist
      | VAR "[" expr "]"          "=" expr     ";" -> block_setlistindex
      | VAR "[" expr "]" OPERATOR "=" expr     ";" -> block_changelistindex
      | "repeat" expr stack -> block_repeat
      | "until"  expr stack -> block_until
      | "forever"     stack -> block_forever
      | "if" expr stack                                   -> block_if
   // | "if" expr stack                      "else" stack -> block_if_else
   // | "if" expr stack ("elif" expr stack)+              -> block_if_elif
   // | "if" expr stack ("elif" expr stack)+ "else" stack -> block_if_elif_else

expr  : "(" expr ")"
      | NUMBER | BOOL | STRING
      | ARG -> arg
      | VAR -> var
      | NAME "(" _exprlist ")" -> reporter
      | "!" expr               -> gnot
      | expr "&" expr -> gand
      | expr "|" expr -> gor
      | expr "<" expr -> gt
      | expr ">" expr -> lt
      | expr "=" expr -> eq
      | expr "+" expr -> add
      | expr "-" expr -> sub
      | expr "*" expr -> mul
      | expr "/" expr -> div

_namelist   : [NAME   ("," NAME  )*]
_stringlist : [STRING ("," STRING)*]
_exprlist   : [expr   ("," expr  )*]

OPERATOR   : /[+\-*\/]/
ARG        : /@[A-Za-z_0-9]*/
VAR        : /[a-zA-Z_.$][A-Za-z_.$0-9]*/
NAME       : /[a-zA-Z_][A-Za-z_0-9]*/
STRING     : /"([^"\\]|\\.)*"/
NUMBER     : /[0-9.]+/
BOOL       : "true" | "false"
ML_COMMENT : /\/\*.*?\*\//s

%ignore ML_COMMENT
%ignore " "
%ignore "\n"
%ignore "\t"
