start: declr*

declr: "costumes" _stringlist ";" -> declr_costumes
     | "globals" _namelist ";" -> declr_globals
     | "listglobals" _namelist ";" -> declr_listglobals
     | "def" NAME _namelist stack -> declr_function
     | "nowarp" "def" NAME _namelist stack -> declr_function_nowarp
     | "on" STRING stack -> declr_on
     | NAME _exprlist stack -> declr_hat

stack: "{" block* "}"

block: NAME "=" expr ";" -> varset
     | "if" expr stack -> block_if
     | "if" expr stack "else" stack -> block_if_else
     | "if" expr stack ("elif" expr stack)+ -> block_if_elif
     | "if" expr stack ("elif" expr stack)+ "else" stack -> block_if_elif_else
     | "until" expr stack -> until
     | "forever" stack -> forever
     | "repeat" expr stack -> repeat
     | NAME "[" "]" ";" -> listset
     | NAME ".add" expr ";" -> listadd
     | NAME ".delete" expr ";" -> listdelete
     | NAME ".insert" expr "," expr ";" -> listinsert
     | NAME "[" expr "]" "=" expr ";" -> listreplace
     | NAME ".show" ";" -> listshow
     | NAME ".hide" ";" -> listhide
     | "..." -> nop
     | NAME _exprlist ";"

expr: "(" expr ")"
    | STRING | NUMBER | FLOAT
    | NAME -> var
    | ARGUMENT -> argument
    | NAME "(" _exprlist ")" -> reporter
    | "not" expr -> notop
    | expr "and" expr -> andop
    | expr "or" expr -> orop
    | expr "=" expr -> eq
    | expr ">" expr -> gt
    | expr "<" expr -> lt
    | expr "+" expr -> add
    | expr "-" expr -> sub
    | expr "*" expr -> mul
    | expr "/" expr -> div
    | expr "%" expr -> mod
    | expr "&" expr -> join
    | NAME "[" expr "]" -> listitem
    | NAME ".index" "(" expr ")" -> listindex
    | NAME ".contains" "(" expr ")" -> listcontains
    | NAME ".length" -> listlength

_exprlist: [expr ("," expr)*]
_stringlist: [STRING ("," STRING)*]
_namelist: [NAME ("," NAME)*]

ARGUMENT: /\$[_a-zA-Z][_a-zA-Z0-9]*/
NAME: /[_a-zA-Z][_a-zA-Z0-9]*/
STRING: /"([^"\\]|\\.)*"/
NUMBER: /-?[0-9]+/
FLOAT: /-?[0-9]+\.[0-9]+/

COMMENT: "'" /.+/ "'"
%ignore COMMENT
%ignore " "
%ignore "\n"
%ignore "\t"
